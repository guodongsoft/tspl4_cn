# 5.1 过程调用

语法: (expr0 expr1 ...)
返回值: expr0的求值结果作为过程被调用，并且把expr1 ...的求值结果作为参数传递给它

Procedure application is the most basic Scheme control structure. Any structured form without a syntax keyword in the first position is a procedure application. The expressions expr0 and expr1 ... are evaluated; each should evaluate to a single value. After each of these expressions has been evaluated, the value of expr0 is applied to the values of expr1 .... If expr0 does not evaluate to a procedure, or if the procedure does not accept the number of arguments provided, an exception with condition type &assertion is raised.

过程调用时最基本的Scheme控制结构。任何在开始位置的非单一关键字语法的结构化形式都是过程调用。首先表达式expr0,expr1 ...被求值；每个表达式都被计算得出一个单一值。这些表达式被求值之后，expr0表达式计算的结果作为过程，expr1 ...等表达式的结果被作为参数传递给它调用执行。如果expr0表达式的结果不是一个过程，或这个过程被传递的参数个数不正确，就会抛出一个条件类型断言的异常。

The order in which the procedure and argument expressions are evaluated is unspecified. It may be left to right, right to left, or any other order. The evaluation is guaranteed to be sequential, however: whatever order is chosen, each expression is fully evaluated before evaluation of the next is started.

过程表达式和参数表达式的求值顺序没有被规定。可能是从左到右，从右到左，或者其他顺序。求值过程能保证被顺序的执行，不管选择哪种顺序执行，每一个表达式都能在下一个表达式被求值之前完成求值过程。

    (+ 3 4) => 7
    ((if (odd? 3) + -) 6 2) => 8
    ((lambda (x) x) 5) => 5
    (let ([f (lambda (x) (+ x x))])
      (f 8)) => 16

过程: (apply procedure obj ... list)
返回值: 调用procedure，并且把obj ... 和list的成员传递给它
库: (rnrs base), (rnrs)

apply invokes procedure, passing the first obj as the first argument, the second obj as the second argument, and so on for each object in obj ..., and passing the elements of list in order as the remaining arguments. Thus, procedure is called with as many arguments as there are objs plus elements of list.

apply调用procedure,第一个obj作为第一个参数，第二个obj作为第二个参数，以此类推，并且把list中的元素作为剩下的参数。就这样，procedure被传递了多个obj加上list的成员作为参数，并且被调用执行。

apply is useful when some or all of the arguments to be passed to a procedure are in a list, since it frees the programmer from explicitly destructuring the list.

当一部分或全部传递给procedure的参数在一个list中的情况下，apply很有用，这样给开发人员提供了使用list的自由度。

(apply + '(4 5)) => 9

(apply min '(6 8 3 2 5)) => 2

(apply min  5 1 3 '(6 8 3 2 5)) => 1

(apply vector 'a 'b '(c d e)) => #(a b c d e)

(define first
  (lambda (ls)
    (apply (lambda (x . y) x) ls)))
(define rest
  (lambda (ls)
    (apply (lambda (x . y) y) ls)))
(first '(a b c d)) => a
(rest '(a b c d)) => (b c d)

(apply append
  '(1 2 3)
  '((a b) (c d e) (f))) => (1 2 3 a b c d e f)
